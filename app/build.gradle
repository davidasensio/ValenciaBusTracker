plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}

// Create a variable called keystorePropertiesFile, and initialize it to your keystore.properties file, in the rootProject folder.
// And load the properties. Now a release bundle could be generated like this: ./gradlew bundleRelease
def KEYSTORE_PATH = "./keystore/keystore_new/keystore_pkcs12.properties"
def keystorePropertiesFile = rootProject.file(KEYSTORE_PATH)
def keystoreProperties = new Properties()
try {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
} catch (Exception e) {
    println("WARNING! Keystore files not found! KeystoreProperties couldn't be loaded.\nCheck filepath: $e.message")
}

android {
    namespace 'com.handysparksoft.valenciabustracker'
    compileSdk 33

    defaultConfig {
        applicationId "com.handysparksoft.valenciabustracker"
        minSdk 26
        targetSdk 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    signingConfigs {
        config {
            try {
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
            } catch (Exception e) {
                println("WARNING! KeystoreProperties not loaded!")
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.3.0'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {
    implementation platform("androidx.compose:compose-bom:2022.10.00")

    implementation "androidx.core:core-ktx:$androidx_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$androidx_lifecycle"
    implementation "androidx.activity:activity-compose:$activity_compose_version"
    implementation "androidx.compose.ui:ui"
    implementation "androidx.compose.ui:ui-tooling-preview"
    implementation "androidx.compose.material3:material3:$material3_version"

    implementation "com.google.android.play:core:$google_play_core_version"
    implementation "com.google.android.play:core-ktx:$google_play_core_ktx_version"

    implementation "com.jakewharton.timber:timber:$timber_version"

    implementation "com.google.firebase:firebase-crashlytics-ktx:18.3.0"
    implementation "com.google.firebase:firebase-analytics-ktx:21.2.0"

    implementation "com.google.android.gms:play-services-ads:21.3.0"

    testImplementation "junit:junit:$test_junit_version"
    androidTestImplementation "androidx.test.ext:junit:$test_ext_junit_version"
    androidTestImplementation "androidx.test.espresso:espresso-core:$test_espresso_version"
    androidTestImplementation "androidx.compose.ui:ui-test-junit4"
    debugImplementation "androidx.compose.ui:ui-tooling"
    debugImplementation "androidx.compose.ui:ui-test-manifest"
}
